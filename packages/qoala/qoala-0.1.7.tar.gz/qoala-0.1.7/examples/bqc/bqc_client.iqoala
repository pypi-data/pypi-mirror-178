
META_START
    name: client
    parameters: server_id, alpha, beta, theta1, theta2, dummy0, dummy1
    csockets: 0 -> server
    epr_sockets: 0 -> server
META_END

csocket = assign_cval() : 0
const_1 = assign_cval() : 1

epr0_rot_y = assign_cval() : 24
epr0_rot_z = assign_cval() : 16
epr0_rot_y = bcond_mult_const(epr0_rot_y, dummy0) : 0
epr0_rot_z = bcond_mult_const(epr0_rot_z, dummy0) : 0
theta2 = bcond_mult_const(theta2, dummy0) : 0

epr1_rot_y = assign_cval() : 24
epr1_rot_z = assign_cval() : 16
epr1_rot_y = bcond_mult_const(epr1_rot_y, dummy1) : 0
epr1_rot_z = bcond_mult_const(epr1_rot_z, dummy1) : 0
theta1 = bcond_mult_const(theta1, dummy1) : 0

run_subroutine(vec<>) : create_epr_0
vec<p2> = run_subroutine(vec<theta2; epr0_rot_y; epr0_rot_z>) : post_epr_0
run_subroutine(vec<>) : create_epr_1
vec<p1> = run_subroutine(vec<theta1; epr1_rot_y; epr1_rot_z>) : post_epr_1

x = mult_const(p1) : 16
minus_theta1 = mult_const(theta1) : -1
delta1 = add_cval_c(minus_theta1, x)
delta1 = add_cval_c(delta1, alpha)

minus_dummy0 = mult_const(dummy0) : -1
should_correct_0 = add_cval_c(const_1, minus_dummy0)
delta1_correction = bcond_mult_const(alpha, should_correct_0) : 0
delta1_correction = mult_const(delta1_correction) : -1
delta1 = add_cval_c(delta1, delta1_correction)

send_cmsg(csocket, delta1)
m1 = recv_cmsg(csocket)

y = mult_const(p2) : 16
minus_theta2 = mult_const(theta2) : -1
beta = bcond_mult_const(beta, m1) : -1
delta2 = add_cval_c(beta, minus_theta2)
delta2 = add_cval_c(delta2, y)

minus_dummy1 = mult_const(dummy1) : -1
should_correct_1 = add_cval_c(const_1, minus_dummy1)
delta2_correction = bcond_mult_const(beta, should_correct_1) : 0
delta2_correction = mult_const(delta2_correction) : -1
delta2 = add_cval_c(delta2, delta2_correction)

send_cmsg(csocket, delta2)
m2 = recv_cmsg(csocket)

return_result(p1)
return_result(p2)
return_result(m1)
return_result(m2)


SUBROUTINE create_epr_0
    params:
    returns:
    request: req0
  NETQASM_START
    array 10 @0
    create_epr C15 C0 C1 C2 C0
    wait_all @0[0:10]
  NETQASM_END

SUBROUTINE post_epr_0
    params: theta2, epr0_rot_y, epr0_rot_z
    returns: M0 -> p2
    request:
  NETQASM_START
    set Q0 0
    rot_z Q0 {theta2} 4
    rot_y Q0 {epr0_rot_y} 4
    rot_z Q0 {epr0_rot_z} 4
    meas Q0 M0
    qfree Q0
  NETQASM_END

SUBROUTINE create_epr_1
    params: 
    returns:
    request: req1
  NETQASM_START
    array 10 @1
    create_epr C15 C0 C4 C5 C3
    wait_all @1[0:10]
  NETQASM_END

SUBROUTINE post_epr_1
    params: theta1, epr1_rot_y, epr1_rot_z
    returns: M1 -> p1
    request:
  NETQASM_START
    set Q0 0
    rot_z Q0 {theta1} 4
    rot_y Q0 {epr1_rot_y} 4
    rot_z Q0 {epr1_rot_z} 4
    meas Q0 M1
    qfree Q0
  NETQASM_END

REQUEST req1
  role: create
  remote_id: {server_id}
  epr_socket_id: 0
  typ: create_keep
  num_pairs: 1
  fidelity: 1.0
  virt_qubit_ids: 0
  result_array_addr: 1

REQUEST req0
  role: create
  remote_id: {server_id}
  epr_socket_id: 0
  typ: create_keep
  num_pairs: 1
  fidelity: 1.0
  virt_qubit_ids: 0
  result_array_addr: 0
